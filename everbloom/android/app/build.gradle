plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace = "com.mycompany.CounterApp"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            // Use environment variables from GitHub Actions if available
            keyAlias System.getenv("KEY_ALIAS") ?: keystoreProperties["keyAlias"]
            keyPassword System.getenv("KEY_PASSWORD") ?: keystoreProperties["keyPassword"]
            storeFile file(keystoreProperties["storeFile"] ?: "app/release-keystore.jks")
            storePassword System.getenv("KEYSTORE_PASSWORD") ?: keystoreProperties["storePassword"]
        }
    }

    defaultConfig {
        applicationId = "com.mycompany.CounterApp"
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources false
        }
    }
}

flutter {
    source = "../.."
}
